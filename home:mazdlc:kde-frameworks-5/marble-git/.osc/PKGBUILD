# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase=marble-git
pkgname=("marble-git" "libastro-git"
         "marble-data-git")
pkgver=1.11.80.r10804.f59d559
pkgrel=1
pkgdesc="Desktop Globe. (GIT version)"
arch=("i686" "x86_64")
url="https://www.kde.org/applications/system/marble"
license=("GPL")
makedepends=("cmake"
             "git"
             "gpsd"
             "quazip-qt5"
             "shapelib"
             "qextserialport-qt5"
             "libwlocate"
             "phonon-qt5"
             "extra-cmake-modules"
             "qt5-webkit"
             "krunner"
             "python"
             "qt5-webkit"
             "qt5-tools"
             "kparts"
             "knewstuff"
             "opencv"
             "hicolor-icon-theme"
             )
source=marble-$pkgver.tar.xz
sha1sums=("SKIP")


prepare() {
  mkdir -p $srcdir/build

  # only build the KDE app
  sed -e "/mobile/d" \
      -e "/qt/d"  \
      -e "/Quick/d" \
      -e "/touch/d" \
      -i $srcdir/marble-$pkgver/src/apps/CMakeLists.txt
}

build() {
  cd $srcdir/build
  cmake ../marble-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DWITH_KF5=ON \
    -DQT_PLUGINS_DIR=lib/qt/plugins \
    -DBUILD_MARBLE_EXAMPLES=ON \
    -DBUILD_MARBLE_TOOLS=ON \
    -DBUILD_MARBLE_TESTS=OFF \
    -DMOBILE=OFF
  make
}

package_libastro-git() {
  pkgdesc="Marble astronomy library"
  depends=("gcc-libs")
  provides=("libastro=15.08")
  conflicts=("kdeedu-marble<15.07"
             "marble-qt"
             "libastro"
             )

  make -C build/src/lib/astro DESTDIR="${pkgdir}" install
}

package_marble-git() {
  depends=("libastro-git"
           "marble-data-git"
           "qt5-webkit"
           "kparts"
           "knewstuff"
           "opencv"
           "quazip-qt5"
           )
  optdepends=("gpsd: position information via gpsd"
              "shapelib: reading and displaying .shp files"
              "qextserialport-qt5: reading from serial port in APRS plugin"
              "libwlocate: Position information based on neighboring WLAN networks"
              "krunner: Krunner plugin for marble"
              )
  provides=("marble=15.08")
  conflicts=("kdeedu-marble<15.04.3-3"
             "marble-qt"
             "marble"
             )
  replaces=("kdeedu-marble"
            "marble-qt<15.07"
            )
  groups=("kde-applications"
          "kdeedu"
          )
  install=marble-git.install

  make -C build DESTDIR="${pkgdir}" install

  # provided by libastro
  rm -r "${pkgdir}/usr/include/astro"
  rm "${pkgdir}/usr/lib/"libastro.*

  # provided by marble-data
  rm -r "${pkgdir}/usr/share/"{icons,marble/data}
}

package_marble-data-git() {
  pkgdesc="Data for Marble"
  arch=("any")
  depends=("hicolor-icon-theme")
  provides=("marble-data=15.08")
  conflicts=("kdeedu-marble<15.07"
             "marble-qt"
             "marble-data"
             )
  install=marble-git.install

  make -C build/data DESTDIR="${pkgdir}" install
}
