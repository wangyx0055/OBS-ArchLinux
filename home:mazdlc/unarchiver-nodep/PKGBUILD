# Maintainer: Cedric Girard <girard.cedric@gmail.com>
# Contributor: N30N <archlinux@alunamation.com>

pkgname=unarchiver-nodep
epoch=1
pkgver=1.9.1
pkgrel=1
pkgdesc="unar and lsar: Objective-C tools for uncompressing archive files"
arch=('x86_64' 'i686')
url="http://unarchiver.c3.cx/"
license=('LGPL2.1')
depends=('openssl' 'bzip2' 'icu' 'gcc-libs' 'zlib' 'avahi' 'gnutls' 'libffi' 'gmp' 'libxslt')
makedepends=('gcc-objc' 'gnustep-base')
provides=("unarchiver=$pkgver")
conflicts=("unarchiver")
source=("http://unarchiver.c3.cx/downloads/unar${pkgver}_src.zip"
        "native_obj_exceptions.patch")
sha1sums=('SKIP'
          'SKIP')

prepare(){
  cd "$srcdir/The Unarchiver"

  patch -p1 < ../native_obj_exceptions.patch

}

build() {
  cd "$srcdir/The Unarchiver/XADMaster"

  . /usr/share/GNUstep/Makefiles/GNUstep.sh
  make -f Makefile.linux
}

package() {
  cd "$srcdir/The Unarchiver/XADMaster"
  install -d "$pkgdir/usr/bin/"
  install -d "$pkgdir/usr/lib/unarchiver/"
  install -m755 unar lsar "$pkgdir/usr/lib/unarchiver/"
  
  cp `ldd "$pkgdir/usr/lib/unarchiver/unar" | grep -oP '/usr/lib/libgnustep-base.so\S*'` "$pkgdir/usr/lib/unarchiver/"
  
  echo '#!/bin/sh' > "$pkgdir/usr/lib/unarchiver/run.sh"
  echo '[[ -z "$LD_LIBRARY_PATH" ]] && \' >> "$pkgdir/usr/lib/unarchiver/run.sh"
  echo 'export LD_LIBRARY_PATH=/usr/lib/unarchiver || \' >> "$pkgdir/usr/lib/unarchiver/run.sh"
  echo 'export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:/usr/lib/unarchiver' >> "$pkgdir/usr/lib/unarchiver/run.sh"
  echo 'exec /usr/lib/unarchiver/`basename "$0"` "$@"'  >> "$pkgdir/usr/lib/unarchiver/run.sh"
  chmod +x "$pkgdir/usr/lib/unarchiver/run.sh"
  ln -s /usr/lib/unarchiver/run.sh "$pkgdir/usr/bin/unar"
  ln -s /usr/lib/unarchiver/run.sh "$pkgdir/usr/bin/lsar"
  
  cd "$srcdir/The Unarchiver/Extra"
  install -d "$pkgdir/usr/share/man/man1"
  gzip -c lsar.1 > "$pkgdir/usr/share/man/man1"/lsar.1.gz
  gzip -c unar.1 > "$pkgdir/usr/share/man/man1"/unar.1.gz
  install -d "$pkgdir/usr/share/bash-completion/completions/"
  install -m644 unar.bash_completion "$pkgdir/usr/share/bash-completion/completions/unar"
  install -m644 lsar.bash_completion "$pkgdir/usr/share/bash-completion/completions/lsar"
}

# vim:set ts=2 sw=2 et:
