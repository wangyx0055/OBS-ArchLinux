# $Id: PKGBUILD 250089 2015-11-03 15:42.05Z foutrelis $
# Maintainer: Thomas Baechler <thomas@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>
# Contributor: Michal Hybner <dta081@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=firefox-i18n
pkgver=42.0
pkgrel=1
pkgdesc="Language pack for Firefox"
arch=('any')
url="http://www.mozilla.com/"
license=('MPL' 'GPL')
depends=("firefox-kde-opensuse>=$pkgver")

_languages=(
  'de     "German"'
  'en-US  "English (US)"'
  'es-ES  "Spanish (Spain)"'
  'fr     "French"'
  'it     "Italian"'
  'ja     "Japanese"'
  'ko     "Korean"'
  'ru     "Russian"'
  'zh-CN  "Chinese (Simplified)"'
  'zh-TW  "Chinese (Traditional)"'
)

pkgname=('firefox-kde-opensuse-i18n-de'
         'firefox-kde-opensuse-i18n-en-US'
         'firefox-kde-opensuse-i18n-es-ES'
         'firefox-kde-opensuse-i18n-fr'
         'firefox-kde-opensuse-i18n-it'
         'firefox-kde-opensuse-i18n-ja'
         'firefox-kde-opensuse-i18n-ko'
         'firefox-kde-opensuse-i18n-ru'
         'firefox-kde-opensuse-i18n-zh-CN'
         'firefox-kde-opensuse-i18n-zh-TW')

source=()
_url=https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/linux-i686/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=firefox-kde-opensuse-i18n-${_locale}
  #printf "'$_pkgname' " >> name.txt
  #pkgname+=($_pkgname)
  source+=("$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for firefox-kde-opensuse"
  _locale_lower=`echo $1 | tr '[:upper:]' '[:lower:]'`
  provides=("firefox-i18n-$1=$pkgver" "firefox-i18n-${_locale_lower}=$pkgver")
  conflicts=("firefox-i18n-$1" "firefox-i18n-${_locale_lower}")
  install -Dm644 $1.xpi \
    "$pkgdir/usr/lib/firefox/browser/extensions/langpack-$1@firefox.mozilla.org.xpi"
}

md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
         'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
