# Author: Jan D. Behrens <zykure@web.de>
# Contributor: doctorkohaku <maido.in.heaven@gmail.com>
# Contributor: Alad Wenter <the.changing.side@gmail.com>
# Maintainer: Jan D. Behrens <zykure@web.de>

pkgbase=deadbeef-plugin-fb-git
pkgname=('deadbeef-plugin-fb-gtk3-git' 'deadbeef-plugin-fb-gtk2-git')
pkgver=20151103
pkgrel=0.86
_pkgdesc="A filebrowser plugin for the DeaDBeeF audio player."
arch=('i686' 'x86_64')
url="https://gitlab.com/zykure/deadbeef-fb"
license=(GPL)
makedepends=('deadbeef' 'gtk3' 'gtk2')
backup=()
source=deadbeef-fb-$pkgver.tar.xz
md5sums=('SKIP')

prepare() {
  cd $srcdir
  rm -rf build-gtk2 build-gtk3
  
  cp -r deadbeef-fb-$pkgver build-gtk2
  cp -r deadbeef-fb-$pkgver build-gtk3
}

build() {
  cd ${srcdir}/build-gtk2
  sh ./autogen.sh
  ./configure --prefix=/usr --enable-gtk2 --disable-gtk3
  make

  cd ${srcdir}/build-gtk3
  sh ./autogen.sh
  ./configure --prefix=/usr --disable-gtk2 --enable-gtk3
  make
}

package_deadbeef-plugin-fb-gtk3-git() {
  pkgdesc="$_pkgdesc For gtk3."
  depends=('deadbeef' 'gtk3')
  provides=deadbeef-plugin-fb-gtk3
  conflicts=deadbeef-plugin-fb-gtk3
  groups=("deadbeef-plugins" "deadbeef-plugins-ui" "deadbeef-plugins-gtk3")
  cd ${srcdir}/build-gtk3
  make DESTDIR="$pkgdir" install
  cd "$pkgdir/usr/share/doc"
  mv deadbeef-fb deadbeef-fb-gtk3
}

package_deadbeef-plugin-fb-gtk2-git() {
  pkgdesc="$_pkgdesc For gtk2."
  depends=('deadbeef' 'gtk2')
  provides=deadbeef-plugin-fb-gtk2
  conflicts=deadbeef-plugin-fb-gtk2
  groups=("deadbeef-plugins" "deadbeef-plugins-ui" "deadbeef-plugins-gtk2")
  cd ${srcdir}/build-gtk2
  make DESTDIR="$pkgdir" install
  cd "$pkgdir/usr/share/doc"
  mv deadbeef-fb deadbeef-fb-gtk2
}

